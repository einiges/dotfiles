#!/usr/bin/env zsh

# zsh version of rifle_sxiv.sh (https://github.com/ranger/ranger/blob/master/examples/rifle_sxiv.sh)
#
# Allows to pass options to sxiv, without editing the script.
# From Terminal run as:
#
#   sxiv_all [SXIV OPTIONS] -- "path/to/an/image"
#
# If arguments are passed in a different form, they will simply getting passed to sxiv.
# For example: $(sxiv_all [SXIV OPTIONS] -- "image1" "image2") will just list the two files, instead
# of the whole directories from image(1|2).
#
# Inside rifle.conf
#
#   mime ^image, has sxiv, X, flag f = path/to/this/script [SXIV OPTIONS] -- "$@"
#

(( $# <= 0 )) && return 1

# If not: sxiv_all [options] -- /path/to/an/image
if (( $# - ${@[(i)--]} != 1 )) || [[ -d "${@[-1]}" ]]; then
	exec sxiv "$@"
fi

unsetopt local_options case_glob

# 0         1..-3    -2 -1
# sxiv_all [options] -- /path/to/an/image
opts=( "${@[1,-3]}" )
target=${@[-1]:A}
files=( ${^target:h}/*.(jp(|e)g|bmp|png|gif|tiff)(.) )

[[ ! $files ]] && return 1

files=( ${(oin)files} )

exec sxiv -n "${files[(Ie)$target]}" "${opts[@]}" -- "${files[@]}"

