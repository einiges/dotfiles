# vim:ft=zsh

if (( ARGC < 2 )); then
	<<- EOF
		Usage: ${0} DESTINATION SOURCE...
	EOF
	return 1
fi

local dest=$1
shift

# Try to be smart and avoid multiple tar levels
function {
	if (( ARGC == 1 )) && [[ $1 =~ '\.tar$' ]]; then
		case "$dest" in
			*.(tar.gz|tgz)      ) gzip  -c "$1" >! "$dest" ;;
			*.(tar.bz2|tbz2|tbz)) bzip2 -c "$1" >! "$dest" ;;
			*.(tar.xz|txz)      ) xz    -c "$1" >! "$dest" ;;
			*.(tar.zma|tlz)     ) lzma  -c "$1" >! "$dest" ;;

			* ) return ;;
		esac
		exit 0
	fi
}

case "$dest" in
	*.tar                   ) tar -cvf  "$dest" "$@" ;;
	*.(tar.gz|tgz)          ) tar -zcvf "$dest" "$@" ;;
	*.(tar.bz2|tbz2|tbz)    ) tar -jcvf "$dest" "$@" ;;
	*.(tar.xz|txz)          ) tar -Jcvf "$dest" "$@" ;;
	*.(tar.zma|tlz)         ) tar --lzma -cvf "$dest" "$@" ;;
	*.gz                    ) gzip     --keep "$@";;
	*.bz2                   ) bzip2    --keep "$@" ;;
	*.xz                    ) xz       --keep "$@" ;;
	*.lzma                  ) lzma     --keep "$@" ;;
	*.zip                   ) zip -r "$dest" "$@" ;;
	*.7z                    ) 7z a -t7z "$dest" "$@" ;;

	* ) print "unsupported archive for $dest" >&2 ;;
esac

